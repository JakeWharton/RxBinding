subprojects {
  buildscript {
    repositories {
      mavenCentral()
    }
  }

  repositories {
    mavenCentral()
  }

  group = GROUP
  version = VERSION_NAME

  // The default 'assemble' task only applies to normal variants. Add test variants as well.
  afterEvaluate {
    android.testVariants.all { variant ->
      tasks.getByName('assemble').dependsOn variant.assemble
    }

    tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
      task.doFirst {
        logging.level = LogLevel.INFO
      }
      task.doLast {
        logging.level = LogLevel.LIFECYCLE
      }
    }
  }
}

ext {
  androidPlugin = 'com.android.tools.build:gradle:1.3.1'
  minSdkVersion = 14
  compileSdkVersion = 23
  buildToolsVersion = '23.0.0'

  ci = 'true'.equals(System.getenv('CI'))

  kotlinPlugin = 'org.jetbrains.kotlin:kotlin-gradle-plugin:0.12.1230'
  kotlinStdlib = 'org.jetbrains.kotlin:kotlin-stdlib:0.12.1230'

  supportAnnotations = 'com.android.support:support-annotations:23.0.1'
  supportV4 = 'com.android.support:support-v4:23.0.1'
  supportRecyclerView = 'com.android.support:recyclerview-v7:23.0.1'
  supportAppCompat = 'com.android.support:appcompat-v7:23.0.1'
  supportDesign = 'com.android.support:design:23.0.1'

  supportTestRunner = 'com.android.support.test:runner:0.3'
  supportTestRules = 'com.android.support.test:rules:0.3'
  supportTestEspresso = 'com.android.support.test.espresso:espresso-core:2.2'
  supportTestEspressoContrib = 'com.android.support.test.espresso:espresso-contrib:2.2'
  rxJava = 'io.reactivex:rxjava:1.0.14'
  rxAndroid = 'io.reactivex:rxandroid:1.0.1'
  junit = 'junit:junit:4.12'
  truth = 'com.google.truth:truth:0.27'
}
