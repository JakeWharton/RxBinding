buildscript {
  ext.buildConfig = [
    'minSdkVersion': 14,
    'compileSdkVersion': 28,
  ]
  ext.versions = [
    'kotlin': '1.3.41',
  ]
  ext.deps = [
    'kotlinStdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",

    'androidXAnnotations': 'androidx.annotation:annotation:1.0.0',
    'androidXAppCompat': 'androidx.appcompat:appcompat:1.0.0',
    'androidXCore': 'androidx.core:core:1.0.0',
    'androidXDrawerLayout': 'androidx.drawerlayout:drawerlayout:1.0.0',
    'androidXLeanback': 'androidx.leanback:leanback:1.0.0',
    'androidXRecyclerView': 'androidx.recyclerview:recyclerview:1.0.0',
    'androidXSlidingPaneLayout': 'androidx.slidingpanelayout:slidingpanelayout:1.0.0',
    'androidXSwipeRefreshLayout': 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0',
    'androidXViewPager': 'androidx.viewpager:viewpager:1.0.0',

    'androidXTestRunner': 'androidx.test:runner:1.1.1',
    'androidXTestRules': 'androidx.test:rules:1.1.1',
    'androidXTestEspresso': 'androidx.test.espresso:espresso-core:3.1.1',
    'androidXTestEspressoContrib': 'androidx.test.espresso:espresso-contrib:3.1.1',

    'googleMaterial': 'com.google.android.material:material:1.0.0',

    'rxJava': 'io.reactivex.rxjava2:rxjava:2.2.10',
    'rxAndroid': 'io.reactivex.rxjava2:rxandroid:2.1.1',

    'junit': 'junit:junit:4.12',
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  afterEvaluate {
    // The default 'assemble' task only applies to normal variants. Add test variants as well.
    def assembleProvider = tasks.named('assemble')
    android.testVariants.all { variant ->
      assembleProvider.configure {
        it.dependsOn(variant.assembleProvider)
      }
    }

    // Disable BuildConfig generation.
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    android.libraryVariants.all {
      it.generateBuildConfigProvider.configure {
        enabled = false
      }
    }

    tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
      task.doFirst {
        logging.level = LogLevel.INFO
      }
      task.doLast {
        logging.level = LogLevel.LIFECYCLE
      }
    }
  }
}
