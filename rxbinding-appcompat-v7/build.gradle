buildscript {
  dependencies {
    classpath rootProject.ext.androidPlugin
  }
}

apply plugin: 'com.android.library'
apply plugin: 'rxbinding-module'

dependencies {
  api project(':rxbinding')
  api rootProject.ext.supportAppCompat
  implementation rootProject.ext.rxAndroid
  implementation rootProject.ext.supportAnnotations

  compileOnly rootProject.ext.autoValueAnnotations
  annotationProcessor rootProject.ext.autoValue

  androidTestImplementation project(':testing-utils')
  androidTestImplementation rootProject.ext.supportTestRunner
  androidTestImplementation rootProject.ext.supportTestRules
  androidTestImplementation rootProject.ext.supportTestEspresso
  androidTestImplementation rootProject.ext.rxAndroid
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion

    testInstrumentationRunner 'com.jakewharton.rxbinding.RxBindingTestRunner'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }
  }

  // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
  libraryVariants.all {
    it.generateBuildConfig.enabled = false
  }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
